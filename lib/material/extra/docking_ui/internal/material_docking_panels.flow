import material/material2tropic;
import material/extra/docking_ui/internal/material_docking_utils;
import material/extra/docking_ui/internal/material_docking_drop;

export {
	// NOTE: example in flow9/lib/material/tests/test_docking_ui.flow

	// Each panel can be movable or docked, and it's defined by:
	MDockingPanel(
		// note: if content is [MDockingPanelToolbarItem] is not used
		title : string,
		// creates easily different standard panels as array of MDockingPanelItems
		content : MDockingPanelContent,
		// store the position where the panel is docked
		panelDock : MDock,
		// starting panel size
		size : WidthHeight,

		
		// panels can be moved and docked on different allowed groups of MDockingAreas, group name's are defined inside each MDockingArea by "groupName", if [] you can dock the panel everywhere
		// allowedGroups : [string],


		// panel's style
		style : [MDockingPanelStyle]
	);
						
	makeMovablePanel(state : MDockingState, panelId : int, cursorPositionA : DynamicBehaviour<[Point]>) -> Material;
	makeDockedPanel(isColumn : bool, state : MDockingState, panelId : int, style : [MDockingStyle]) -> Material;
	// addDockedTopbar(isColumn : bool, state : MDockingState, panelId : int, pTheme : bool, style : [MDockingStyle]) -> Material;
}


makeDockingPanelContent(state : MDockingState, panelId : int, mobilePanel : bool, isForcedHorizontal : bool, resizePressed : DynamicBehaviour<bool>, isHovering : DynamicBehaviour<bool>, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	panelContent = getValue(state.content)[panelId];
	dontShowSep = contains(style, MDPanelHideToolbarSeparators());
	dontResize  = contains(style, MDPanelDontResize());
	isToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbar([]));
	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	theme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;

	resizeHandleMobile = MInteractive(
		[
			TMouseDown(resizePressed), 
			TMouseInside(isHovering, false)
		],
		makeMDockingResizeHandleMobile(state, style)
	);

	content =
		switch (panelContent : MDockingPanelContent) {

			MDPanelToolbar(buttonsA): {

				mapi(buttonsA, \i, btn : MDPanelToolbarItem -> {
					click  = extractStruct(btn.style, MOnClick(nop)).click;
					pad = 10.0;
					minWidth = dockingItemHeight;
					sep = if (dontShowSep) TEmpty() else MSeparator(!isForcedHorizontal);
					buttonSep = if (i == length(buttonsA) - 1) TEmpty() else {if (dontShowSep) TEmpty() else sep}

					ic = MCursor(FingerCursor(), MIcon(btn.icon, [MIconSize(dockingToolbarIconSize), FillOpacity(dockingToolbarIconOpacity)]));
					button = MRawButton(ic, ic, ic, ic, "", [], [TOnClicked(click)]);

					b = if (isForcedHorizontal) MCols2A(MCenterIn(button, TFillWY(minWidth)), buttonSep)
							else MLines2(MCenterIn(button, TFillXH(minWidth)), buttonSep);

					if (!isForcedHorizontal) MBorder(pad, 0.0, pad, 0.0, b) else MBorder(0.0, pad, 0.0, pad, b);


				}) |> (\m -> if (isForcedHorizontal) MColsA(m) else MLines(m))
			}
			MDPanelContent(mat) : mat;
		};

	MGroup([
		content,
		if (mobilePanel) {if (dontResize || isToolbar) TEmpty() else resizeHandleMobile;} else TEmpty()
	])
	|> (\f -> (MExplicitTheme(f, theme, [])));
}

makePanelTopBar(docked : bool, panelId: int, state : MDockingState, isColumn: bool, pTheme : bool) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	enabled = getValue(state.enabled)[panelId];
	expanded = getValue(state.expanded)[panelId];
	sideExpanded = getValue(state.sideExpanded)[panelId];
	position = getValue(state.positions)[panelId];
	title = getValue(state.title)[panelId];
	minTitleEllipsisWidth = extractStruct(pstyle, MDPanelTitleEllipsis(120.0)).width;
	isToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbar([]));
	// Size
	size = if (docked) getValue(state.dockedSize)[panelId] else getValue(state.mobileSize)[panelId];
	width = size.width;
	height = size.height;
	isForcedHorizontal = contains(pstyle, MDPanelForceHorizontalWhenMobile());
	topBarSize = makeHSize(if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel, isColumn || !isToolbar);
	// Prop
	disableButton = contains(pstyle, MDPanelShowCloseBtn());
	disableExpander = contains(pstyle, MDPanelHideExpanderBtn());
	// Colors
	barColor : MColor = topBarColorFn(pTheme, pstyle);
	titleColor : MColor = titleColorFn(pTheme, pstyle);

	addTooltip = \label : string, m : Material -> MTooltip(m , MText(label, [MTooltipDesktop()]), []);
	getPanelIcon = \st : [MIconStyle] ->  {
		icon = extractStruct(pstyle, MDPanelIcon("web_asset")).icon;
		MIcon(icon, concat([titleColor], st));
	}

	makeIconToggleButton = \boolB : DynamicBehaviour<bool>,
		iconOn : string, iconOff : string,
		tooltipOn : string, tooltipOff :string,
		iconSize: double, clickFn : () -> void,
		iconColor : MColor, fill : double -> {
			MSelect(boolB,\b -> if (b) MIconButton(iconOn, clickFn, [MIconSize(iconSize), iconColor, FillOpacity(fill), MIconButtonBorder(1.0)], []) |> (\f -> addTooltip(tooltipOn, f))
				else MIconButton(iconOff, clickFn, [MIconSize(iconSize), iconColor, FillOpacity(fill), MIconButtonBorder(1.0)], []) |> (\f -> addTooltip(tooltipOff, f))
			) |> MCenterY
	}

	// Panel's buttons
	customButtonsA = extractStruct(pstyle, MDPanelCustomButtons([])).buttons;
	customButtons = customButtonsA != [];
	panelButtonsA = map(customButtonsA, \btn -> {
		clickedB = make(false);
		switch (btn) {
			MDPanelCustomButton(ic, tt, clkFn, st) : {
				btnColor = extractStruct(st, titleColor);
				makeIconToggleButton(clickedB, ic, ic, tt, tt, dockingTopBarIconSize, clkFn, btnColor, 0.5);
			}
			default: MEmpty();
		} |> MCenterY

	});

	// Handles
	linedHandle = makeMDockingLinedHandle(isForcedHorizontal, titleColor, isToolbar);
	dottedHandle =  makeMDockingDottedHandle(isColumn, titleColor, isToolbar);

	// TopBar Content
	panelTopBarButtons = {
		MBaselineColsA([
			getPanelIcon([MIconSize(dockingTopBarIconSize), FillOpacity(dockingTitleOpacity)])
				|> MBorderLeft(10.0)
				|> MCenterY,
			MEllipsisText(title, [titleColor, dockingTitleFont, MMinWidth(minTitleEllipsisWidth)]) 
			|> MBorderLeft(10.0) 
			|> MCenterY,
			TFillX(),
			if (customButtons)
				MBaselineColsA(panelButtonsA) |> MCenterY
			else TEmpty(),
			if (disableButton)
				MCols2A(
					TFixed(6.0, 0.0),
					MIconButton("close", \ -> next(enabled, false), [MIconSize(dockingTopBarIconSize - 4.0), titleColor, MIconButtonBorder(1.0), FillOpacity(0.5)], [])
						|> (\f -> addTooltip("hide panel", f)) |> MCenterY
				)
			else TEmpty(),
			TFixed(4.0, 0.0),

			if (disableExpander)
				TEmpty()
			else
				makeIconToggleButton(
					expanded,
					"keyboard_arrow_up",
					"keyboard_arrow_down",
					"collapse panel",
					"expand panel",
					dockingTopBarIconSize,
					\ -> {
						reverseBehaviour(expanded);
						// next(state.dock, getValue(state.dock)); //force redraw (should be used for mobile)
					},
					titleColor,
					0.5
				) |> showIf(isColumn),


			TFixed(8.0, 0.0)
		])
	}

	dockedTopBar = \isSideExpanded : bool  -> {
		MAvailable(
			MGroup([
				// Topbar background
				if (isToolbar) {
					TRounded(dockingRadius, 0.0, 0.0, dockingRadius, [MFill(barColor)], makeHSize(dockingBarHeightToolbar, isColumn))
				} else {

					if (isColumn) {
						if (isSideExpanded) {
							MIfLazy(expanded,\e -> 
								if (e) TRounded(dockingRadius, dockingRadius, 0.0, 0.0, [MFill(barColor), FillOpacity(1.0)], TFillXH(dockingBarHeightPanel))
									else TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(barColor)], TFillXH(dockingBarHeightPanel))
							) //|> showIf(!contains(pstyle, MDPanelDontShowTopbar()));
						} else {
							TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(barColor)], TFillXH(dockingBarHeightPanel))
								|> (\f -> addTooltip(title + " panel", f))
						}

					} else {				
						TRectangle([MFill(barColor), /*FillOpacity(0.0)*/], TFillXH(dockingBarHeightPanel))
					}
				},
				// Topbar content
				if (isToolbar) {
					MGroup2(
						TRectangle([MFill(panelBgColorFn(pTheme, pstyle))], topBarSize),
						MCenterIn(
							dottedHandle|> (\m -> if (isColumn) MBorderTop(4.0)(m) else MBorderLeft(4.0)(m)), // tweak handle position for toolbar content
							topBarSize
						)
					)
				} else {
						if (isSideExpanded) panelTopBarButtons else {			
							getPanelIcon([MIconSize(sideExpanderButtonSize), FillOpacity(0.3)]) |> MCenter
						}
				}
			]), topBarSize
		);
	};

	mobileTopBar =
		if (isForcedHorizontal) {
			MSelect(expanded,\e ->
				MAvailable(
					MGroup([
						if (e) TRounded(dockingRadius, 0.0, 0.0, dockingRadius, [MFill(barColor)], TFillXY())
						else TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(barColor)], TFillXY()),
						MCenterIn(linedHandle, TFixed(dockingBarHeightPanel, width)),
					]),
					TFixed(dockingBarHeightPanel, width)
				)
			);
		} else {
			MAvailable(
				MGroup([
					MSelect(expanded,\e -> if (e) TRounded(dockingRadius, dockingRadius, 0.0, 0.0, [MFill(barColor)], TFillXY())
						else TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(barColor)], TFillXY())	
					),
					if (isSameStructType(getValue(state.content)[panelId], MDPanelToolbar([])))
						MCenterIn(linedHandle, TFillXH(dockingBarHeightPanel))
					else panelTopBarButtons			
				]),
				TFillXH(dockingBarHeightPanel)
			);
		}

	MSelect(getValue(state.sideExpanded)[panelId], \se -> {
		if (se) {
			if (docked) dockedTopBar(se) else mobileTopBar;
		} else {
			dockedTopBar(se);
		}
	});
}

makeMovablePanel(state : MDockingState, panelId : int, cursorPositionA : DynamicBehaviour<[Point]>) -> Material {
	manager = state.manager;
	itemId = makeTropicTag(manager);
	dockA = state.dock;
	groupA = state.group;
	enabled = getValue(state.enabled)[panelId];
	expanded = getValue(state.expanded)[panelId];
	position = getValue(state.positions)[panelId];
	pstyle = getValue(state.panelStyle)[panelId];
	isForcedHorizontal = contains(pstyle, MDPanelForceHorizontalWhenMobile());
	cacheEnabled = extractStruct(state.style, MDockingEnableLoadAndStoreCache("", false)).cacheEnabled;
	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	mobileTheme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;

	resizePressed = make(false);
	isResizing = make(false);
	isHovering = make(false);
	dContent = makeDockingPanelContent(state, panelId, true, isForcedHorizontal, resizePressed, isHovering, pstyle);

	isToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbar([]));
	topBarH = if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel;

	// Mobile size
	size = getValue(state.mobileSize)[panelId];
	width = size.width;
	height = size.height + topBarH;

	disableButton = contains(pstyle, MDPanelShowCloseBtn());

	mouseXyLocal = make(zeroPoint);
	dragPressed = make(false);
	tempWH = make(WidthHeight(width, height));
	r = dockingRadius;

	draggingShape = TSelect(state.mobileSize, \ms ->
		if (getValue(getValue(state.expanded)[panelId])) {
			TRounded(r, r, r, r, [MFill(dockingPrimaryColor /*panelBgColorFn(fgetValue(mobileTheme), pstyle)*/), FillOpacity(dockingMobileOpacity)], TFixed(ms[panelId].width, ms[panelId].height))
		} else TRounded(r, r, r, r, [MFill(dockingPrimaryColor), FillOpacity(dockingMobileOpacity)], TFixed(ms[panelId].width, dockingBarHeightPanel))
	) |> (\m -> if (isForcedHorizontal) TRotate90(m) else m);

	makeResizablePanel = \c : Material, lg : bool -> {	
		MConstruct([
				makeSubscribe(isResizing, \rs -> {
					if (!rs) {
					// update mobile size
					next(state.mobileSize, replace(getValue(state.mobileSize), panelId , getValue(tempWH))); //
				}
			})
		],
		MInteractive([TMouseXY(mouseXyLocal)],
			// TODO: Toolbar item options
			if (isForcedHorizontal) {
				MShadow(const(4.0),
				MColsA([
					makePanelTopBar(false, panelId, state, !isForcedHorizontal, lg),
					MSelect(expanded,\e ->
						if (e)
							MAvailable(
								MGroup([
									// TRectangle([MFill(areaBgColorFn(lg, pstyle))], size |> TRotate90 ),
									MCenterIn(c, TFixed(size.width, size.height) |> TRotate90)// ,
								]),
								TFixed(size.width, size.height) |> TRotate90
							)
						else TEmpty()
					)
				]));
			} else {
				MShadow(const(4.0),
					MSelect(expanded,\e -> {
						MAvailable(
							MLines2(
								makePanelTopBar(false, panelId, state, !isForcedHorizontal, lg),
								if (e) c else TEmpty() 
							) |> (\f -> MFrame(0.0, r, [MFill(panelBgColorFn(lg, pstyle))], f)),
							if (e) TSized(tempWH) else TSelect(tempWH, \wh ->  TFixed(wh.width, dockingBarHeightPanel)),
						)
					}),
				)
			}
		))
	}

	makeMovablePanel = \lg -> 
		MDraggable(panelId,
			MSelect(manager.manager.dragging, \h -> {
				d = MConstruct([
					makeSubscribe(dragPressed, \p -> {
						if (p) next(cursorPositionA, replace(getValue(cursorPositionA), panelId, getValue(mouseXyLocal)));
					})],
					makeResizablePanel(dContent, lg)
				);
				if (h == panelId) draggingShape else d;
				// d
			})|> (\f-> MInteractive([TMouseDownGhost(dragPressed)],f)),
			[]
		);




	MConstruct([
		makeSubscribe2(isResizing, \rs -> { 
			if (!rs) {
				// Update mobile size
				updateH(state.mobileSize, panelId,  getValue(tempWH).height - topBarH);
				updateW(state.mobileSize, panelId,  getValue(tempWH).width);
				// Update docked size
				updateH(state.dockedSize, panelId,  getValue(tempWH).height - topBarH);
				updateW(state.dockedSize, panelId,  getValue(tempWH).width);

				// Update cache
				if (cacheEnabled) setMDockingStateKeyValue(state) |> ignore;
			}
		}),
		makeSubscribe(resizePressed, \p -> {
			if (p) {
				addMInteractionItemId(manager, itemId);
				next(isResizing, true);
			} else {
				if (getValue(isResizing)) next(isResizing, false);
			}
		}),
		makeSubscribe(mouseXyLocal, \mxy -> {
			if (isCurrentMInteractionItemId(manager, itemId) && getValue(isResizing)) {
				blockOtherMInteractionItemIds(manager, itemId);
				if (mxy.y >= dockingBarHeightPanel) nextDistinct(tempWH, WidthHeight(getValue(tempWH).width, mxy.y)) else nextDistinct(tempWH, WidthHeight(getValue(tempWH).width, height));
				if (mxy.x >= dockingBarHeightPanel) nextDistinct(tempWH, WidthHeight(mxy.x, getValue(tempWH).height)) else nextDistinct(tempWH, WidthHeight(width, getValue(tempWH).height));
			}
		})
	],	
	MSelect2(dockA, mobileTheme, \d, lg -> {
		if (d[panelId].areaId < 0) {
			next(getValue(state.sideExpanded)[panelId], true);
			MSelect(enabled, \e -> if (e) 
				MTranslate(
					const(getValue(position)), 
					makeMovablePanel(lg)
				) else TEmpty()
			);
		} else MEmpty();	
	}))
}

makeDockedPanel(isColumn : bool, state : MDockingState, panelId : int, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	isToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbar([]));

	// Scroll
	dontScroll  = contains(pstyle, MDPanelDontScroll());
	hasScrollPos = \dockingstyle -> {
		!exists([MDPanelScrollPosition(make(zeroPoint))], \dsp -> isSameStructType(dsp, dockingstyle))
	};
	scrollPos = extractStruct(style, MDPanelScrollPosition(make(zeroPoint))).scrollPos;
	hasScrollInspectVisible = \dockingstyle -> {
		!exists([MDPanelScrollInspectVisible(make(zeroPoint), makeWH())], \dsiv -> isSameStructType(dsiv, dockingstyle))
	};
	scrollTopLeft = extractStruct(style, MDPanelScrollInspectVisible(make(zeroPoint), makeWH())).scrollTopLeft;
	scrollWidthHeight = extractStruct(style, MDPanelScrollInspectVisible(make(zeroPoint), makeWH())).scrollWidthHeight;

	// Expanded
	expanded = getValue(state.expanded)[panelId];
	sideExpanded = getValue(state.sideExpanded)[panelId];
	enabled = getValue(state.enabled)[panelId];
	
	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	dockedTheme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;


	// println("  -   -   -   -   -   -   -   -   - > DockedPanel " + i2s(panelId));


	// Panel's content
	dContent = makeDockingPanelContent(state, panelId, false, !isColumn, make(false), make(false), pstyle);
	dPanel = \lg : bool -> {
		if (isToolbar) {
			dContent 
			|> (\f -> MGroup2(TFillXY(), f))
			|> (\f -> MFrame(0.0, dockingRadius, [MFill(panelBgColorFn(lg, pstyle))], f))  
		} else {
			
			MSelect(expanded, \e -> {	
				pContent = if (dontScroll) {
					MCropSize(TFillXY(), dContent)
				} else {
					MScroll(
						dContent, TFillXY(),
						if (hasScrollPos(style) && hasScrollInspectVisible(style))
							[MScrollPosition(scrollPos), TScrollInspectVisible(scrollTopLeft, scrollWidthHeight)]
						else if (hasScrollPos(style))
							[MScrollPosition(scrollPos)]
						else []
					)
				}
				if (isColumn) {
					if (e) pContent else TFillXH(dockingCollapsedSeparatorH);
				} else	pContent;

			}) 
			|> (\f -> MFrame(0.0, dockingRadius, [MFill(panelBgColorFn(lg, pstyle))], f))
			|> (\m -> if (!isColumn) m else MShowLazy(sideExpanded, \ -> m))
		} 
	}

	MSelect(dockedTheme, \lg -> {
		addRowTopBars = \f : Material -> [addDockedTopbar(isColumn, state, panelId, lg, style), f]
			|> (\r -> if (isColumn) MLines(r) else {if (isToolbar) MCols(r) else MLines(r)});
		dPanel(lg) 
			|> addRowTopBars
	})
}


addDockedTopbar(isColumn : bool, state : MDockingState, panelId : int, pTheme : bool, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	dontMove  = contains(pstyle, MDPanelDontMove());
	isToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbar([]));
	panelDockedSize = getValue(state.dockedSize)[panelId];
	r = dockingRadius;
	enabled = getValue(state.enabled)[panelId];


	dheight = if (isColumn) panelDockedSize.height else panelDockedSize.width;
	// dheight = if (isColumn) panelDockedSize.height else {
	// 	if (isToolbar) panelDockedSize.height else panelDockedSize.width
	// };
	posCorrection = Point(0.0, 0.0);
		// if (isColumn) Point(0.0, 0.0) else {
		// 	dx = 4.0; 
		// 	dy = (dheight / 2.0) - 12.0; 
		// 	Point(dx, -dy);
		// }

	// Mobile size for dragging shape
	mHeight = getValue(state.mobileSize)[panelId].height;
	mWidth = getValue(state.mobileSize)[panelId].width;
	panelSize = TFixed(mWidth, mHeight);

	draggingShape = 
		TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(dockingPrimaryColor), FillOpacity(dockingMobileOpacity)], panelSize) 
			|> (\m -> 
				if (isToolbar) {
					if (isColumn) m else TRotate90(m)
				} else m
			);

	cursor = fif(fselect(manager.manager.dragging, FLift(\dr -> dr >= 0)), const(GrabbingCursor()), const(GrabCursor()));
	// MSelect(theme, \lg -> {
		// Test for removing top bar
		// MTooltip(
		// 	// MBorder4(12.0, MIcon("trip_origin", [MIconSize(dockingTopBarIconSize - 8.0), FillOpacity(0.9), titleColorFn(lg, [])])),
		// 	MBorder4(12.0, MIcon("trip_origin", [MIconSize(dockingTopBarIconSize - 8.0), FillOpacity(0.9), MYellow(500)])),
		// 	MText("Undock Panel", []),
		// 	[]
		// ) 	
		makePanelTopBar(true, panelId, state, isColumn, pTheme) 
			|> (\f -> if (dontMove) f else
				MDraggable(panelId, f, [
					TDragShape(TTranslate(const(posCorrection), draggingShape)),
					TCursorShape(DynamicCursor(cursor))
				])
			)
	// })
}
